##
## $HOME/.zshrc runs after /etc/zshrc if the shell is an interactive shell
## This is Zsh's main configuration file
##

#-----------------------------------------------------------
# oh-my-zsh
#-----------------------------------------------------------
# Path to oh-my-zsh configuration
export ZSH=~/.config/oh-my-zsh

ZSH_THEME="re5et_dlee440"
plugins=(colorize git github zsh-syntax-highlighting)
source $ZSH/oh-my-zsh.sh


#-----------------------------------------------------------
# Base16 Shell Colors
#-----------------------------------------------------------
BASE16_SCHEME="ashes"
BASE16_SHELL="$HOME/.config/base16-shell/base16-$BASE16_SCHEME.dark.sh"
[[ -s $BASE16_SHELL ]] && . $BASE16_SHELL


#-----------------------------------------------------------
# completion styles
#-----------------------------------------------------------
zstyle ':completion:*' menu select
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}

# list of completers to use
zstyle ':completion:*' completer _complete _ignored

# match uppercase from lowercase
zstyle ':completion:*' matcher-list '' 'm:{[:lower:]}={[:upper:]}' 'r:|[._-]=* r:|=*'

# formatting and messages
zstyle ':completion:*' verbose yes
zstyle ':completion:*:descriptions' format '%B%d%b'
zstyle ':completion:*:messages' format '%d'
zstyle ':completion:*:warnings' format 'No matches for: %d'
zstyle ':completion:*:corrections' format '%B%d (errors: %e)%b'
zstyle ':completion:*' group-name ''

## add colors to processes for kill completion
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
zstyle ':completion:*:*:kill:*:processes' command 'ps --forest -A -o pid,user,cmd'
zstyle ':completion:*:processes-names' command 'ps axho command'

# New completion:
# 1. All /etc/hosts hostnames are in autocomplete
# 2. If you have a comment in /etc/hosts like #%foobar.domain,
# then foobar.domain will show up in autocomplete!
zstyle ':completion:*' hosts $(awk '/^[^#]/ {print $2 $3" "$4" "$5}' /etc/hosts | grep -v ip6- && grep "^#%" /etc/hosts | awk -F% '{print $2}')

# http://www.sourceguru.net/ssh-host-completion-zsh-stylee/
zstyle -e ':completion::*:*:*:hosts' hosts 'reply=(${=${${(f)"$(cat {/etc/ssh_,~/.ssh/known_}hosts(|2)(N) /dev/null)"}%%[# ]*}//,/ })'

# SSH Completion
zstyle ':completion:*:scp:*' tag-order files users 'hosts:-host hosts:-domain:domain hosts:-ipaddr"IP Address *'
zstyle ':completion:*:scp:*' group-order files all-files users hosts-domain hosts-host hosts-ipaddr
zstyle ':completion:*:ssh:*' tag-order users 'hosts:-host hosts:-domain:domain hosts:-ipaddr"IP Address *'
zstyle ':completion:*:ssh:*' group-order hosts-domain hosts-host users hosts-ipaddr
zstyle '*' single-ignored show

zstyle :compinstall filename '~/.zshrc'

# autoload -Uz compinit
autoload -U compinit promptinit colors
compinit
promptinit
colors

# unsetopt beep notify
setopt no_beep        # no beeps on error
setopt autocd         # cd to directory without cd
setopt extendedglob   # treat #, ~, and ^ as part of patterns for filename generation
setopt nomatch        # if a pattern for filename generation has no matches, print error instead of leaving it unchanged in arg list
# setopt noclobber      # if you want to clobber a file use >! operator
setopt completealiases      # prevent aliases from being internally substituted before completion is attempted
setopt append_history       # allow multiple terminal sessions to all append to one zsh command history
setopt extended_history     # save timestamps of command and duration
setopt inc_append_history   # add commands as they're typed, don't wait until shell exit
setopt hist_ignore_all_dups # older duplicates removed
setopt hist_ignore_space
setopt hist_find_no_dups
setopt share_history        # imports new commands and appends typed commands to history
setopt hist_verify          # don't execute, just expand to buffer
export HISTFILE=~/.zsh_history
export HISTSIZE=1000000
export SAVEHIST=$HISTSIZE


#-----------------------------------------------------------
# ZLE, zsh command line editor
#-----------------------------------------------------------
bindkey -v                          # Vi(m) mode
bindkey -M viins 'jj' vi-cmd-mode

bindkey "\e[A" up-line-or-beginning-search
bindkey "\e[B" down-line-or-beginning-search
# bindkey '^R' history-incremental-search-backward
# bindkey "^j" history-beginning-search-backward
# bindkey "^k" history-beginning-search-forward

autoload up-line-or-beginning-search
autoload down-line-or-beginning-search

zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search


#-----------------------------------------------------------
# window title
#-----------------------------------------------------------
case $TERM in
  # *xterm|xterm-256color|rxvt|rxvt-unicode|rxvt-256color|rxvt-unicode-256color|(dt|k|E)term)
  *xterm*|*rxvt*|(dt|k|E|a)term)
    precmd () { print -Pn "\e]0;%n@%M [%~] \a" }
    preexec () { print -Pn "\e]0;%n@%M [%~] ($1)\a" }
  ;;
esac

#PS1='%(?.%F{green}.%F{red})%#%f '
#RPS1='%B%F{blue}%~%f%b'


#-----------------------------------------------------------
# aliases
#-----------------------------------------------------------
# enable color support
alias dir='dir --color=auto'
alias vdir='vdir --color=auto'
alias grep='grep --color=auto'
alias fgrep='fgrep --color=auto'
alias egrep='egrep --color=auto'
alias diff='colordiff'

# miscellaneous shortcuts
alias vi='vim'
alias svim='sudo vim'
alias nano='nano -w'          # -w --nowrap, don't wrap long lines
alias more='less'
alias cls='clear'
alias hist='history | tail'
alias hgrep='history | grep'  # grep commands from running shell
alias ping='ping -c 3'        # -c count, stop after snd/rcv # pkts
alias pgt='ping gatech.edu'
alias wget='wget -c'          # -c --continue, continue downloading remote file instead of duplicate <file>.1
alias so='source ~/.zshrc'
alias da='date "+%A, %B, %d, %Y [%T]"'
# alias open='xdg-open'
# alias k='ssh-add'           # adds private key identities to the authentication agent
alias m='make'
alias mc='make clean'
alias codepad='wgetpaste -C'
alias ch='chmod 755'

# ls
alias ls='ls -hF --color=auto --group-directories-first'
alias la='ls -A'
alias ll='ls -l'
alias lla='ls -Al'
alias lr='ls -R'
alias ld='ll -rct'      # reverse order (oldest/largest first)
alias lz='ll -rS'       # sort by size
alias lra='lr -A'
alias lm='ll | more'
alias lma='lla | more'
alias l='ls -CF'        # list by columns and append indicator to entires

# safety: interactive prompt before action, verbose
alias cp='cp -i'
alias mv='mv -i'
alias rm="rm -i"
alias mkdir='mkdir -pv' # -p create intermediate directories

# cd
alias ..='cd ../'
alias ...='cd ../../'
alias ....='cd ../../../'
alias .....='cd ../../../..'
alias ......='cd ../../../../..'

# system operations
alias reboot='sudo reboot'
alias shutdown='sudo shutdown -h now'
alias df='df -h'        # free disk space, -h human readable (bytes)
alias du='du -c -h'     # disk usage stats, -c show total, -h human readable
alias pg='ps -af | grep -v grep | grep $1' # process state (enter command for $1?)
alias nports='netstat --all --numeric --programs --inet --inet6'
alias sports='ss --all --numeric --processes --ipv4 --ipv6'

#alias mkdir and cd
function mkdircd () { mkdir -p "$@" && eval cd "\"\$$#\""; }
function cdl () { cd "$@" && la; }


#-----------------------------------------------------------
# functions
#-----------------------------------------------------------
extract ()
{
  if [ -f $1 ] ; then
    case $1 in
      *.tar.bz2)  tar xvjf $1     ;;
      *.tar.gz)   tar xvzf $1     ;;
      *.bz2)      bunzip2 v $1    ;;
      *.rar)      unrar ev $1     ;;
      *.gz)       gunzip $1       ;;
      *.tar)      tar xvf $1      ;;
      *.tbz2)     tar xvjf $1     ;;
      *.tgz)      tar xzvf $1     ;;
      *.zip)      unzip $1        ;;
      *.Z)        uncompress v $1 ;;
      *.7z)       7z x $1         ;;
      *)          echo "'$1' cannot be extracted" ;;
    esac
  else
    echo "'$1' is not a valid file"
  fi
}

# config files
conf()
{
  case $1 in
    tmux)       vim ~/.tmux.conf ;;
    vim)        vim ~/.vimrc ;;
    xinit)      vim ~/.xinitrc ;;
    xres)       vim ~/.Xresources && xrdb ~/.Xresources ;;
    zsh)        vim ~/.zshrc && source ~/.zshrc ;;
    hosts)      sudoedit /etc/hosts ;;
    *)          echo "Unknown application: $1" ;;
  esac
}

# Less colors for man pages
man()
{
  env \
  LESS_TERMCAP_mb=$'\E[01;31m' \
  LESS_TERMCAP_md=$'\E[01;38;5;74m' \
  LESS_TERMCAP_me=$'\E[0m' \
  LESS_TERMCAP_se=$'\E[0m' \
  LESS_TERMCAP_so=$'\E[38;5;246m' \
  LESS_TERMCAP_ue=$'\E[0m' \
  LESS_TERMCAP_us=$'\E[04;38;5;146m' \
  man "$@"
}

